repositories {
    mavenCentral()
}

apply plugin: 'java-library'

if ( !hasProperty( 'buildNumber' ) ) {
    ext.buildNumber = defaultBuildNumber
}

version = '1.0.0'

def vertxVersion = "4.1.3"
def es4xVersion = "0.15.0"
def junitVersion = "5.7.2"
def junitPlatformVersion = "1.7.2"
// this version matches the one defined in es4x 0.15.0
def graalVmVersion = "21.1.0"

dependencies {

    // if you're on a plain JDK, you need to declare these ones
    implementation "org.graalvm.truffle:truffle-api:$graalVmVersion"
    implementation "org.graalvm.js:js:$graalVmVersion"
    implementation "org.graalvm.tools:profiler:$graalVmVersion"
    implementation "org.graalvm.tools:chromeinspector:$graalVmVersion"
    // if you're on a real graalvm (you don't need them and they should not be defined as it will collide with
    // the graalvm ones)

    // You will need these ones in order to generate es4x modules from vert.x codegen java APIs
    compileOnly "io.vertx:vertx-codegen:$vertxVersion"
    annotationProcessor "io.vertx:vertx-codegen:$vertxVersion:processor"
    annotationProcessor "io.reactiverse:es4x-codegen:$es4xVersion"

    implementation 'org.slf4j:slf4j-api:1.7.26'
    implementation "io.vertx:vertx-core:$vertxVersion"
    implementation "io.reactiverse:es4x:$es4xVersion"

    runtimeOnly 'ch.qos.logback:logback-classic:1.2.3'
    runtimeOnly 'ch.qos.logback:logback-core:1.2.3'

    testImplementation "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testImplementation "org.junit.vintage:junit-vintage-engine:$junitVersion"
    testImplementation "org.junit.platform:junit-platform-launcher:$junitPlatformVersion"
    testImplementation "org.junit.platform:junit-platform-runner:$junitPlatformVersion"
    testImplementation "io.vertx:vertx-unit:$vertxVersion"
    testImplementation "io.vertx:vertx-junit5:$vertxVersion"
    testImplementation 'org.assertj:assertj-core:3.20.2'
}

tasks.withType(JavaCompile).configureEach {
    javaCompiler = javaToolchains.compilerFor {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

tasks.withType(Test).configureEach {
    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

// include this when generating java language classes that need compilation
// this is a no-op if there's no java codegen
sourceSets {
    vertxPolyglot{
        java.srcDir "${buildDir.canonicalPath}/generated-sources/vertx"
    }
    main {
        java {
            srcDir "${buildDir.canonicalPath}/generated-sources/vertx"
        }
    }
}

apply from: "es4x-codegen.gradle"
task generatePolyglotApis(type: JavaCompile) {
    group 'build'
    description 'Generates Vertx js and rxjava apis'
    source = sourceSets.main.java
    options.annotationProcessorPath = configurations.annotationProcessor
    classpath = configurations.compileClasspath
    options.debugOptions.debugLevel = "source,lines,vars"
    options.compilerArgs = [
            "-proc:only",
            "-processor", "io.vertx.codegen.CodeGenProcessor",
            "-Acodegen.output=${buildDir}/generated-sources/vertx"
    ]
    destinationDir file("${buildDir}/generated-sources/vertx")
}

compileJava {
    dependsOn(generatePolyglotApis)
    options.debugOptions.debugLevel = "source,lines,vars"
    source += sourceSets.vertxPolyglot.java // compile generated java classes, if any
    options.compilerArgs += '-proc:none'
}

test {
    useJUnitPlatform()
}
